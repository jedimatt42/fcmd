MEMORY
{
  cart_rom   (rx) : ORIGIN = 0x6000, LENGTH = 0x0100 /* 256 bytes up front of common code */
  bank_rom   (rx) : ORIGIN = 0x6100, LENGTH = 0x1F00 /* 8k-header of bankable cartridge ROM */
  lower_exp  (wx) : ORIGIN = 0x2000, LENGTH = 0x2000 /* 8k        */
  upper_exp  (wx) : ORIGIN = 0xA000, LENGTH = 0x6000 /* 24k upper ram */
  scratchpad (wx) : ORIGIN = 0x8320, LENGTH = 0x00e0 /* 32b is for workspace */
}


SECTIONS
{
  /* common */
  .text : {
    __header.o(.text)
    banking.o(.text)
    bankdata.o(.text)
  } >cart_rom

  /* overlays */
  OVERLAY : 
  {
    .bank0 { 
      _crt0.o(.text)
      main.o(.text) 
      __LOAD_DATA = .;
    }
    .bank1 { 
      counter.o(.text) 
    }
  } >bank_rom

  . = 0x2000;
  .data : {
    __DATA_START = .;
    * (.data); 
    __DATA_END = .;
  } >lower_exp

  .bss  ALIGN(2) : { __BSS_START = .; *(.bss); __BSS_END = .;} >lower_exp
}  
