MEMORY
{
  cart_rom   (rx) : ORIGIN = 0x6000, LENGTH = 0x0070 /* 96 bytes up front of common code */
  bank_rom   (rx) : ORIGIN = 0x6070, LENGTH = 0x1F90 /* 8k-header of bankable cartridge ROM */
  lower_exp  (wx) : ORIGIN = 0x2000, LENGTH = 0x2000 /* 8k        */
  upper_exp  (wx) : ORIGIN = 0xA000, LENGTH = 0x4000 /* 16k upper ram */
  libti99_rom  (rx) : ORIGIN = 0xE000, LENGTH = 0x2000 /* 8k top of upper ram */
  scratchpad (wx) : ORIGIN = 0x8320, LENGTH = 0x00e0 /* 32b is for workspace */
}


SECTIONS
{
  /* common */
  .text : {              /* all modules listed here will end up at front of each ROM bank */
    objects/header.o(.text)      
    objects/trampoline.o(.text)
    objects/bankdata.o(.text)
    str_memcpy.o(.text)  /* gcc will implicity call this to initialize strings on stack */
  } >cart_rom

  /* overlays */
  OVERLAY : AT (0x10000)
  {
    .bank0 {             /* list the set of modules in bank 0 */
      objects/b0_*.o(.text)
      __LOAD_DATA = .;   /* .data segment is copied here so initial values can be loaded in RAM */
    }
    .bank1 { 
      objects/b1_*.o(.text)
      __LIB_COPY = .;
    }                    /*   modules should be linked into the same bank */
    .bank2 { 
      objects/b2_*.o(.text)
    }                    /*   modules should be linked into the same bank */
    .bank3 { 
      objects/b3_*.o(.text)
    }                    /*   modules should be linked into the same bank */
  } >bank_rom

  .libti99 : {
    __LIB_START = .;     /* note the address so we can copy starting from here in ROM */
    libti99.a(.text)     /* vdp routines from libti99 */
    objects/b1cp_*.o(.text) 
  } >libti99_rom

  . = 0xA000;            /* remaining references will start from this address. */
  .data : {
    __DATA_START = .;    /* define RAM location symbol so __LOAD_DATA can be copied from ROM */
    * (.data); 
    __DATA_END = .;      /* identifies end of data for initialization routine. */
  } >upper_exp

  .bss  ALIGN(2) : { 
    __BSS_START = .;     /* _crt0 will initialize RAM from here to __BSS_END to 0x00 values */
    *(.bss); 
    __BSS_END = .;
  } >upper_exp

  __STACK_TOP = 0x4000;

  __STATS_HEAD = SIZEOF(.text);
  __STATS_BANK_0 = 0x1F90 - SIZEOF(.bank0);
  __STATS_BANK_1 = 0x1F90 - SIZEOF(.bank1) - SIZEOF(.libti99);
  __STATS_LIBTI99 = SIZEOF(.libti99);
  __STATS_BANK_2 = 0x1F90 - SIZEOF(.bank2);
  __STATS_BANK_3 = 0x1F90 - SIZEOF(.bank3);
}  
