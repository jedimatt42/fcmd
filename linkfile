MEMORY
{
  cart_rom   (rx) : ORIGIN = 0x6000, LENGTH = 0x0070 /* 96 bytes up front of common code */
  bank_rom   (rx) : ORIGIN = 0x6070, LENGTH = 0x1F90 /* 8k-header of bankable cartridge ROM */
  lower_exp  (wx) : ORIGIN = 0x2000, LENGTH = 0x2000 /* 8k        */
  upper_exp  (wx) : ORIGIN = 0xA000, LENGTH = 0x6000 /* 24k upper ram */
  scratchpad (wx) : ORIGIN = 0x8320, LENGTH = 0x00e0 /* 32b is for workspace */
}


SECTIONS
{
  /* common */
  .text : {              /* all modules listed here will end up at front of each ROM bank */
    header.o(.text)      
    trampoline.o(.text)
    bankdata.o(.text)
    str_memcpy.o(.text)  /* gcc will implicity call this to initialize strings on stack */
  } >cart_rom

  /* overlays */
  OVERLAY : 
  {
    .bank0 {             /* list the set of modules in bank 0 */
      crt0.o(.text)
      main.o(.text) 
      globals.o(.text)
      parsing.o(.text)
      getstr.o(.text)
      strutil.o(.text)
      __LOAD_DATA = .;   /* .data segment is copied here so initial values can be loaded in RAM */
    }
    .bank1 { 
      libti99.a(.text)     /* vdp routines from libti99 */
      dsrutil.o(.text)
      lvl2.o(.text)
      tifloat.o(.text)
      mds_dsrlnk.o(.text)
      mds_dsrlnkraw.o(.text)
    }                    /*   modules should be linked into the same bank */
    .bank2 { 
      commands.o(.text)
    }                    /*   modules should be linked into the same bank */
    .bank3 { 
      help.o(.text)
      oem.o(.text)
      oem_high_chars.o(.text)
      oem_low_chars.o(.text)
    }                    /*   modules should be linked into the same bank */
  } >bank_rom

  . = 0x2000;            /* remaining references will start from this address. */
  .data : {
    __DATA_START = .;    /* define RAM location symbol so __LOAD_DATA can be copied from ROM */
    * (.data); 
    __DATA_END = .;      /* identifies end of data for initialization routine. */
  } >lower_exp

  .bss  ALIGN(2) : { 
    __BSS_START = .;     /* _crt0 will initialize RAM from here to __BSS_END to 0x00 values */
    *(.bss); 
    __BSS_END = .;
  } >lower_exp

  __STACK_TOP = 0x4000;
}  
